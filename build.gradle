plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'org.openjfx.javafxplugin' version '0.0.10'
    id 'org.beryx.jlink' version '2.26.0'
}

group 'io.vproxy'
version '1.3.5'

repositories {
    mavenLocal()
    mavenCentral()
}

sourceCompatibility = '11'
targetCompatibility = '11'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

java {
    withSourcesJar()
    withJavadocJar()
}

javafx {
    version = '11'
    modules = ['javafx.controls', 'javafx.swing', 'javafx.media']
}

dependencies {
    api group: 'com.github.kwhat', name: 'jnativehook', version: '2.2.2'
    api group: 'io.vproxy', name: 'commons', version: '1.3.4'
    testImplementation group: 'junit', name: 'junit', version: '4.13.2'
}

javadoc {
    exclude '**/*.java'
}

task runTest(type: JavaExec) {
    classpath = sourceSets.test.runtimeClasspath
    main = "io.vproxy.vfx.test.TestMain"
    systemProperty("IsDebug", System.getProperty("IsDebug", "false"))
    args = ["io.vproxy.vfx.test." + System.getProperty("TestClass")]
}

task runIntro(type: JavaExec) {
    classpath = sourceSets.test.runtimeClasspath
    main = "io.vproxy.vfx.intro.IntroMain"
}

tasks.withType(GenerateModuleMetadata) {
    enabled = false
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            artifactId 'vfx'

            pom {
                name = 'vfx'
                description = 'javafx library'
                url = 'http://vfx.vproxy.io'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://github.com/wkgcass/vfx/blob/master/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'wkgcass'
                        name = 'K.G. Wang'
                        email = 'wkgcass@hotmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/wkgcass/vfx.git'
                    developerConnection = 'scm:git:git://github.com/wkgcass/vfx.git'
                    url = 'https://github.com/wkgcass/vfx'
                }
            }
            pom.withXml {
                def pomNode = asNode()
                pomNode.dependencies.'*'.findAll() {
                    it.groupId.text() == 'org.openjfx'
                }.each {
                    it.parent().remove(it)
                }
            }
        }
    }
    repositories {
        maven {
            credentials {
                username 'wkgcass'
                password System.getProperty("MavenPublishPassword")
            }
            def releasesRepoUrl = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
            def snapshotsRepoUrl = 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
            url = version.contains('-DEV') ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
}

signing {
    if (!System.getProperty("MavenLocalInstall", "false").equals("true")) {
        sign publishing.publications.maven
    }
}
